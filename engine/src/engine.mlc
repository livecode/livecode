/* Copyright (C) 2003-2013 Runtime Revolution Ltd.
 
 This file is part of LiveCode.
 
 LiveCode is free software; you can redistribute it and/or modify it under
 the terms of the GNU General Public License v3 as published by the Free
 Software Foundation.
 
 LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
 WARRANTY; without even the implied warranty of MERCHANTABILITY or
 FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 for more details.
 
 You should have received a copy of the GNU General Public License
 along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/*
This library provides operations for interacting with LiveCode Script from LiveCode Builder.

Tags: Engine

Name: ScriptObject

Type: type

Summary: An opaque type corresponding to a LiveCode script object.

Syntax: ScriptObject

Description:
Use the <ResolveScriptObject|resolve script object>, or <MyScriptObject|my script object> to obtain an object of type <ScriptObject>

References: ResolveScriptObject(statement), MyScriptObject(expression)
*/

module com.livecode.engine

use com.livecode.foreign

public foreign type ScriptObject binds to "kMCEngineScriptObjectTypeInfo"

public foreign handler MCEngineExecResolveScriptObject(in pObjectId) as ScriptObject binds to "<builtin>"
public foreign handler MCEngineEvalScriptObjectExists(in pObject as ScriptObject, out rExists as bool) as undefined binds to "<builtin>"
public foreign handler MCEngineEvalScriptObjectDoesNotExists(in pObject as ScriptObject, out rExists as bool) as undefined binds to "<builtin>"
public foreign handler MCEngineExecGetPropertyOfScriptObject(in pProperty as string, in pObject as ScriptObject) as any binds to "<builtin>"
public foreign handler MCEngineExecSetPropertyOfScriptObject(in pProperty as string, in pObject as ScriptObject, in pValue as any) as undefined binds to "<builtin>"
public foreign handler MCEngineExecDispatchToScriptObject(in pIsFunction as bool, in pMessage as string, in pTarget as ScriptObject) as any binds to "<builtin>"
public foreign handler MCEngineExecDispatchToScriptObjectWithArguments(in pIsFunction as bool, in pMessage as string, in pTarget as ScriptObject, in pArguments as optional list) as any binds to "<builtin>"
public foreign handler MCEngineEvalMessageWasHandled(out pHandled as bool) as undefined binds to "<builtin>"
public foreign handler MCEngineEvalMessageWasNotHandled(out pHandled as bool) as undefined binds to "<builtin>"
public foreign handler MCEngineExecExecuteScript(in pScript as string) as any binds to "<builtin>"
public foreign handler MCEngineExecLog(in pFormat as string) as undefined binds to "<builtin>"
public foreign handler MCEngineExecLogWithValues(in pFormat as string, in pValues as list) as undefined binds to "<builtin>"

/*
Summary:	Resolves a string to a script object.
Object:		The string describing the script object.

The result:	The resolved script object. 

Example:
	variable tObject as ScriptObject
	resolve script object "button 1 of card 1 of this stack"
	put the result into tObject
	if tObject exists then
		get property "name" of tObject
		log the result
	else
		log "No such button"
	end if	
	
Description:
Use the <ResolveScriptObject|resolve script object> statement to obtain a <ScriptObject> in LiveCode Builder, in order to interact with it.

*/

syntax ResolveScriptObject is statement
    "resolve" "script" "object" <Object: Expression>
begin
    MCEngineExecResolveScriptObject(Object)
end syntax

/*
Summary:	Tests the existence of a script object.
Object:		An expression that evaluates to a <ScriptObject>.

The result:	True if the object <Object> exists, and false otherwise.

Example:
	variable tObject as ScriptObject
	resolve script object "card 5 of this stack"
	put the result into tObject
	if tObject exists then
		set property "name" of tObject to "Card5"
	else
		log "No such card"
	end if	
	
Description:
Use to test the existence or otherwise of a script object, for example after attempting to resolve the object from a string using <ResolveScriptObject | resolve script object>

References: ResolveScriptObject(statement)
*/

syntax ScriptObjectExists is postfix operator with precedence 3
    <Object: Expression> "exists"
begin
    MCEngineEvalScriptObjectExists(Object, output)
end syntax

/*
Summary:	Tests the existence of a script object.
Object:		An expression that evaluates to a <ScriptObject>.

The result:	True if the object <Object> exists, and false otherwise.

Example:
	variable tObject as ScriptObject
	resolve script object "card 5 of this stack"
	put the result into tObject
	if tObject does not exist then
		log "No such card"
	end if	
	
Description:
Use to test the non-existence or otherwise of a script object, for example after attempting to resolve the object from a string using <ResolveScriptObject|resolve script object>

References: ResolveScriptObject(statement)
*/

syntax ScriptObjectDoesNotExist is postfix operator with precedence 3
    <Object: Expression> "does" "not" "exist"
begin
    MCEngineEvalScriptObjectDoesNotExists(Object, output)
end syntax

/*
Summary:	Get a property of a script object.
Property: 	The name of the property to set.
Object:		An expression that evaluates to a <ScriptObject>.

The result:	The value of the property.

Example:
	public handler myScript() as string
		get property "script" of my script object
		return the result
	end handler
	
Description:
Use to fetch properties of a script object. 

>*Note:* An error is thrown if the script object does not exist.
*/

syntax GetPropertyOfScriptObject is statement
    "get" "property" <Property: Expression> "of" <Object: Expression>
begin
    MCEngineExecGetPropertyOfScriptObject(Property, Object)
end syntax

/*
Summary:	Set a property of a script object.
Property: 	The name of the property to set.
Object:		An expression that evaluates to a <ScriptObject>.
Value: 		The value to set the property to.

Example:
	public handler OnMouseUp() as undefined
		set property "name" of my script object to "Clicked Widget"
	end handler

Example:
	resolve script object "this stack"
	set property "invisible" of the result to true
	
Description:
Use to set properties of a script object. 

>*Note:* An error is thrown if the script object does not exist.
*/

syntax SetPropertyOfScriptObject is statement
    "set" "property" <Property: Expression> "of" <Object: Expression> "to" <Value: Expression>
begin
    MCEngineExecSetPropertyOfScriptObject(Property, Object, Value)
end syntax

/*
Summary: 	Dispatch a message to a script object.
Message: 	The message to dispatch.
Object:		The script object to dispatch the message to.
Arguments:	A list of arguments for the message.

The result:	The result of the message dispatch

Example:
	public handler OnMouseUp() as undefined
		// Send a message to the script so it can handle it.
		dispatch "buttonClicked" to my script object
	end handler
	
Example:
	// myScriptFunction takes three arguments and performs some kind of calculation

	variable tObject as ScriptObject
	resolve script object "this stack"
	put the result into tObject
	dispatch function "myScriptFunction" to tObject with [ 2, 3, 4 ]
	
	// the result contains the return value of 'myScriptFunction'
*/

syntax DispatchToScriptObject is statement
    "dispatch" ( "function" <IsFunction=true> | "command" <IsFunction=false> | <IsFunction=false> ) <Message: Expression> "to" <Object: Expression> [ "with" <Arguments: Expression> ]
begin
    MCEngineExecDispatchToScriptObject(IsFunction, Message, Object)
    MCEngineExecDispatchToScriptObjectWithArguments(IsFunction, Message, Object, Arguments)
end syntax

/*
Summary: 	Determines whether a message was handled

Example:
	public handler OnMouseUp() as undefined
		dispatch "buttonClicked" to my script object
		if the message was handled then
			return
		end if	
		log "The message was not handled"
	end handler
	
Description:
Determines if a dispatched message was handled by any script objects in the message path.
*/

syntax MessageWasHandled is expression
    "the" "message" "was" "handled"
begin
    MCEngineEvalMessageWasHandled(output)
end syntax

/*
Summary: 	Determines whether a message was handled

Example:
	public handler OnMouseUp() as undefined
		dispatch "buttonClicked" to my script object
		if the message was not handled then
			log "The message was not handled"
		end if	
	end handler
	
Description:
Determines if a dispatched message was handled by any script objects in the message path.
*/

syntax MessageWasNotHandled is expression
    "the" "message" "was" "not" "handled"
begin
    MCEngineEvalMessageWasNotHandled(output)
end syntax

/*
Summary: 	Executes some LiveCode script.
Script: 	The script to execute.

Example:
	public handler SnapshotMe() as undefined
		variable tVar as string
		get property "number" of my script object
		put the result formatted as string into tVar

		// Create a snapshot from the rect of the widget
		execute script "import snapshot from rect (the rect of widget " & tVar & ") of widget " & tVar
	end handler

Description:
Determines if a dispatched message was handled by any script objects in the message path.
*/

syntax ExecuteScript is statement
    "execute" "script" <Script: Expression>
begin
    MCEngineExecExecuteScript(Script)
end syntax

/*
Summary: 	Logs a string.
String:		The string to log.
Arguments:	A list of arguments.

Example:
	variable tList as list
	get property "name" of my script object
	push the result onto tList
	
	get property "id" of my script object
	push the result onto tList
	
	log "Widget %@ has id %@" with tList

Description:
The log command triggers a logChanged message with the accumulated log messages since the logChanged message was last handled.
This can be handled in LiveCode Script, for example:
```
on logChanged pLog
	write pLog to stdout
end logChanged
```

*/

syntax Log is statement
    "log" <String: Expression> [ "with" <Arguments: Expression> ]
begin
    MCEngineExecLog(String)
    MCEngineExecLogWithValues(String, Arguments)
end syntax

end module
