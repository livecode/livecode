/*
Copyright (C) 2018 LiveCode Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/**
A library of utility handlers for functions commonly needed by Android widgets.
*/

module com.livecode.library.androidutils

use com.livecode.foreign
use com.livecode.java
use com.livecode.canvas
use com.livecode.library.widgetutils

metadata version is "1.0.0"
metadata author is "LiveCode"
metadata title is "Android Utilities"
metadata os is "android"

private handler ColorComponentToInt(in pComponent as Real) returns Integer
    multiply pComponent by 255
    round pComponent 
    return pComponent
end handler

__safe foreign handler _JNI_GetColorFromARGB(in pA as JInt, in pR as JInt, in pG as JInt, in pB as JInt) returns JInt \
	binds to "java:android.graphics.Color>argb(IIII)I!static"

/**
Summary: Convert a color string to an integer for Android

Parameters:
pString: The color string

Description:
Use the <StringToAndroidColor> handler to convert a string representing 
a color to an integer that can be used with Android color APIs.
*/	
public handler StringToAndroidColor(in pString as String) returns Integer
    variable tColor as Color
    put stringToColor(pString) into tColor
    
    variable tA as Integer
    variable tR as Integer
    variable tG as Integer
    variable tB as Integer
    put ColorComponentToInt(the alpha of tColor) into tA
    put ColorComponentToInt(the red of tColor) into tR
    put ColorComponentToInt(the green of tColor) into tG
    put ColorComponentToInt(the blue of tColor) into tB
    
    return _JNI_GetColorFromARGB(tA,tR,tG,tB)
end handler

__safe foreign handler _JNI_GetAndroidEngine() returns JObject \
	binds to "java:com.runrev.android.Engine>getEngine()Lcom/runrev/android/Engine;!static"
__safe foreign handler _JNI_GetEngineContext(in pEngine as JObject) returns JObject \
	binds to "java:android.view.View>getContext()Landroid/content/Context;"

/**
Summary: Get the application Context

Example:
	__safe foreign handler _JNI_CreateButton(in pContext as JObject) returns JObject \
		binds to "java:android.widget.Button>new(Landroid/content/Context;)?ui"

	handler NativeButtonView() returns JObject
		return _JNI_CreateButton(ApplicationContext())
	end handler

Description:
Use the <ApplicationContext> handler to fetch the current application's
Context object. 
*/
public handler ApplicationContext() returns JObject
	return _JNI_GetEngineContext(_JNI_GetAndroidEngine())
end handler

__safe foreign handler _JNI_CreateContextThemeWrapper(in pContext as JObject, in pThemeId as JInt) returns JObject \
	binds to "java:android.view.ContextThemeWrapper>new(Landroid/content/Context;I)"
	
__safe foreign handler _JNI_DefaultDeviceTheme() returns JInt \
	binds to "java:android.R$style>get.Theme_DeviceDefault()I!static"
	
/**
Summary: Get a Context wrapper with the default device theme

Example:
	__safe foreign handler _JNI_CreateButton(in pContext as JObject) returns JObject \
		binds to "java:android.widget.Button>new(Landroid/content/Context;)?ui"

	handler NativeButtonViewDefaultTheme() returns JObject
		return _JNI_CreateButton(DefaultDeviceThemeContext())
	end handler

Description:
Use the <DefaultDeviceThemeContext> handler to fetch a Context object
that wraps the application context, but with the default device theme.

This is useful when creating widgets that should match the default theme
of whatever device the app is being run on.
*/
public handler DefaultDeviceThemeContext() returns JObject
	return _JNI_CreateContextThemeWrapper(ApplicationContext(), \
		_JNI_DefaultDeviceTheme())
end handler
end module
