/*
Copyright (C) 2017 LiveCode Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/**
Name: Mac Native Button

Description: 
This widget is a native push button on Mac.

Name: enabled

Syntax: 
set the enabled of <widget> to {true | false}
get the enabled of <widget>

Description:
Use the <enabled> property to enable or disable the native button. When
disabled, the button has a greyed out appearance and does not accept 
clicks or touches.
*/
widget com.livecode.widget.native.mac.button

use com.livecode.foreign
use com.livecode.objc
use com.livecode.widget
use com.livecode.canvas
use com.livecode.engine
use com.livecode.library.widgetutils

metadata version is "1.0.0"
metadata author is "LiveCode"
metadata title is "Mac Native Button"
metadata svgicon is "m -19.375905,-4.8110434 c -1.108001,0 -2,0.892 -2,2 l 0,15.4453124 c 0,1.108 0.891999,2 2,2 l 27.5722648,0 c 1.108,0 2.0000002,-0.892 2.0000002,-2 l 0,-15.4453124 c 0,-1.108 -0.8920002,-2 -2.0000002,-2 l -27.5722648,0 z m 6.857421,2.3847656 13.8593751,0 c 0.588,0 1.0664531,0.4812187 1.0644531,1.0742188 l 0,9.8437501 c 0,0.594 -0.4764531,1.074219 -1.0644531,1.074219 l -5.3203125,0 c 0,0 0.5936093,2.1484369 3.22460934,2.1484369 l 0,0.537109 -3.49218744,0 -4.5683598,0 -1.8789057,0 0,-0.537109 c 2.5589997,0 3.4921873,-2.1484369 3.4921873,-2.1484369 l -5.3164063,0 c -0.589,-10e-7 -1.066406,-0.480219 -1.066406,-1.074219 l 0,-9.8437501 c 0,-0.593 0.477406,-1.0742188 1.066406,-1.0742188 z m 0.07617,1.1425781 0,9.1074219 13.7070312,0 0,-9.1074219 -13.7070312,0 z m 7.8105468,1.12890629 c 0.04,0.364 -0.1062656,0.7291875 -0.3222656,0.9921875 -0.217,0.26200001 -0.5709219,0.46450001 -0.9199219,0.43750001 -0.047,-0.35600001 0.128125,-0.72598441 0.328125,-0.95898441 0.225,-0.262 0.6020625,-0.4577031 0.9140625,-0.4707031 z m -0.00586,1.50195311 c 0.202,0.008 0.7707688,0.080281 1.1347656,0.6132813 -0.028,0.019 -0.6779219,0.3956406 -0.6699219,1.1816406 0.009,0.938 0.8220313,1.2508594 0.8320313,1.2558594 -0.008,0.021 -0.1296882,0.4448126 -0.4296876,0.8828125 -0.2579999,0.3770001 -0.5262187,0.7537187 -0.9492187,0.7617187 -0.415,0.008 -0.5474375,-0.2460937 -1.0234375,-0.2460937 -0.475,0 -0.6245782,0.2379062 -1.0175781,0.2539062 -0.4080001,0.015 -0.7185154,-0.4081561 -0.9785159,-0.7851562 -0.533,-0.7709999 -0.941531,-2.1769532 -0.394531,-3.1269532 0.272,-0.471 0.7581563,-0.7693437 1.2851562,-0.7773437 0.4,-0.007 0.7794376,0.2695313 1.0234376,0.2695313 0.246,0 0.7054999,-0.3322032 1.1875,-0.2832032 z M -5.5887964,8.147941 c -0.331,0 -0.5996094,0.2676561 -0.5996094,0.5976561 0,0.331 0.2686093,0.599609 0.5996094,0.599609 0.33,10e-7 0.5986093,-0.268609 0.5996093,-0.599609 10e-8,-0.33 -0.2696093,-0.5976561 -0.5996093,-0.5976561 z"

constant kSvgIcon is "M14.926,0.656H1.067C0.478,0.656,0,1.137,0,1.73v9.844c0,0.594,0.478,1.074,1.067,1.074h5.316 c0,0-0.934,2.149-3.493,2.149v0.537h1.88h4.568h3.493v-0.537c-2.631,0-3.226-2.149-3.226-2.149h5.32 c0.588,0,1.065-0.48,1.065-1.074V1.73C15.992,1.137,15.514,0.656,14.926,0.656z M7.996,12.427c-0.331,0-0.599-0.268-0.599-0.599 c0-0.33,0.268-0.598,0.599-0.598c0.33,0,0.599,0.268,0.599,0.598C8.594,12.159,8.326,12.427,7.996,12.427z M14.849,10.906H1.143 V1.798H14.85L14.849,10.906L14.849,10.906zM6.824,9.133c0.393-0.016,0.542-0.255,1.017-0.255c0.476,0,0.609,0.255,1.024,0.247 C9.288,9.117,9.556,8.74,9.814,8.363c0.3-0.438,0.423-0.862,0.431-0.883c-0.01-0.005-0.824-0.317-0.833-1.255 c-0.008-0.786,0.642-1.163,0.67-1.182C9.718,4.51,9.149,4.437,8.947,4.429C8.465,4.38,8.005,4.713,7.759,4.713 c-0.244,0-0.623-0.277-1.023-0.27C6.209,4.451,5.724,4.75,5.452,5.221c-0.547,0.95-0.14,2.356,0.393,3.127 C6.105,8.725,6.416,9.148,6.824,9.133z M8.953,2.928c-0.312,0.013-0.689,0.208-0.914,0.47c-0.2,0.233-0.376,0.604-0.329,0.96 c0.349,0.027,0.704-0.177,0.921-0.439C8.847,3.656,8.993,3.292,8.953,2.928z"

/**
Syntax:
set the label of <widget> to <pLabel>
get the label of <widget>

Summary: The label displayed by the button.

Value (string): The string to use as the button label

Example:
    set the label of widget "Mac Button" to "Click me!"

Description:
The <label> property is the label displayed by the button.
*/

property label get mLabel set SetLabel
metadata label.editor is "com.livecode.pi.string"
metadata label.default is ""

private variable mLabel as String
private variable mButtonView as optional ObjcObject
private variable mButtonProxy as optional ObjcObject

private handler IsMac() returns Boolean
    return the operating system is "mac"
end handler

/**/

public handler OnCreate()
	put "" into mLabel
end handler

public handler OnDestroy()
	put nothing into mButtonView
end handler

/**/

public handler OnOpen()
    if IsMac() then
        unsafe
            CreateButtonView()
        end unsafe
    end if
end handler

public handler OnClose()
    if IsMac() then
        unsafe
            DestroyButtonView()
        end unsafe
    end if
end handler

/**/

constant kBorderWidth is 5
private handler expandRectangle(in pRect as Rectangle, in pExp as Number) returns Rectangle
    return rectangle [ the left of pRect - pExp, the top of pRect - pExp, the right of pRect + pExp, the bottom of pRect + pExp]
end handler

public handler OnPaint()
	if IsMac() then
		return
	end if

    // Draw placeholder image - the icon
    variable tFillPaint as Paint
    variable tStrokePaint as Paint
    put solid paint with color [0.875, 0.875, 0.875] into tFillPaint
    put solid paint with color [0.75, 0.75, 0.75] into tStrokePaint
    variable tBounds as Rectangle
    put my bounds into tBounds
    set the paint of this canvas to tFillPaint
    fill rectangle path of tBounds on this canvas
    set the paint of this canvas to tStrokePaint
    set the stroke width of this canvas to kBorderWidth
    set the join style of this canvas to "bevel"
    stroke rectangle path of expandRectangle(tBounds, -kBorderWidth / 2) on this canvas
    variable tPath as Path
    put path kSvgIcon into tPath
    constrainPathToRect(expandRectangle(tBounds, -2 * kBorderWidth), tPath)
    fill tPath on this canvas
    // Draw the control name
    put solid paint with color [1, 1, 1, 0.1] into tFillPaint
    put solid paint with color [0.25, 0.25, 0.25] into tStrokePaint
    variable tNameString as String
    if mLabel is empty then
        put my name into tNameString
    else
        put mLabel into tNameString
    end if
    set the font of this canvas to my font
    
    variable tTextBounds as Rectangle
    put the image bounds of text tNameString on this canvas into tTextBounds
    translate this canvas by [ the width of tBounds / 2, the height of tBounds / 2]
    translate this canvas by [ -(the width of tTextBounds / 2), the height of tTextBounds / 2]
    set the paint of this canvas to tFillPaint
    
    variable tRect as Rectangle
    put expandRectangle(tTextBounds, kBorderWidth) into tRect
    fill rounded rectangle path of tRect with radius 5 on this canvas
    set the paint of this canvas to solid paint with color [0.0, 0.0, 0.0]
    fill text tNameString at center of tRect on this canvas
end handler

/**/

public handler OnSave(out rProperties as Array)
	put mLabel into rProperties["label"]
end handler

public handler OnLoad(in pProperties as Array)
	put pProperties["label"] into mLabel
end handler

/**/

public handler OnParentPropertyChanged()
    unsafe
        UpdateButtonView()
    end unsafe
end handler

/**/

private handler SetLabel(in pLabel as String) returns nothing
	put pLabel into mLabel
	unsafe
		UpdateButtonView()
	end unsafe
    redraw all
end handler

/****/

private handler ButtonActionCallback(in pSender as ObjcObject, in pContext as optional any) returns nothing
	post "mouseUp"
    
    // Wake the engine to deal with the posted signal
    MCEngineRunloopBreakWait()
end handler

/****/

private type NSUInteger is CULong
private foreign handler ObjC_NSButtonAlloc() returns ObjcRetainedId binds to "objc:NSButton.+alloc"
private foreign handler ObjC_NSButtonInit(in pObj as ObjcId) returns ObjcId binds to "objc:NSButton.-init"
private foreign handler ObjC_NSButtonSetEnabled(in pObj as ObjcId, in pEnabled as CBool) returns nothing binds to "objc:NSButton.-setEnabled:"
private foreign handler ObjC_NSButtonSetFont(in pObj as ObjcId, in pFont as ObjcId) returns nothing binds to "objc:NSButton.-setFont:"
private foreign handler ObjC_NSButtonSetButtonType(in pObj as ObjcId, in pStyle as NSUInteger) returns nothing binds to "objc:NSButton.-setButtonType:"
private foreign handler ObjC_NSButtonSetBezelStyle(in pObj as ObjcId, in pStyle as NSUInteger) returns nothing binds to "objc:NSButton.-setBezelStyle:"
private foreign handler ObjC_NSButtonSetBordered(in pObj as ObjcId, in pBordered as CBool) returns nothing binds to "objc:NSButton.-setBordered:"
private foreign handler ObjC_NSButtonSetTitle(in pObj as ObjcId, in pTitle as ObjcId) returns nothing binds to "objc:NSButton.-setTitle:"
private foreign handler ObjC_NSButtonSetTarget(in pObj as ObjcId, in pTarget as ObjcId) returns nothing binds to "objc:NSButton.-setTarget:"
private foreign handler ObjC_NSButtonSetAction(in pObj as ObjcId, in pAction as UIntPtr) returns nothing binds to "objc:NSButton.-setAction:"

private foreign handler MCCanvasFontGetHandle(in pFont as Font, out rHandle as ObjcId) returns nothing binds to "<builtin>"

private unsafe handler CreateButtonView()
	variable tButtonView as ObjcObject
	put ObjC_NSButtonAlloc() into tButtonView
	put ObjC_NSButtonInit(tButtonView) into tButtonView

    /* For a standard push button we need:
     *   buttonType to be NSMomentaryPushInButton = 7
     *   bezelStyle to be NSRoundedBezelStyle = 1
     *   bordered to be true */
    ObjC_NSButtonSetButtonType(tButtonView, 7)
    ObjC_NSButtonSetBezelStyle(tButtonView, 1)
    ObjC_NSButtonSetBordered(tButtonView, true)

    set my native layer to PointerFromObjcObject(tButtonView)
	put tButtonView into mButtonView

	put ObjcProxyGetTarget(ButtonActionCallback, nothing) into mButtonProxy
	ObjC_NSButtonSetTarget(mButtonView, mButtonProxy)
	ObjC_NSButtonSetAction(mButtonView, ObjcProxyGetAction())

	UpdateButtonView()
end handler

private unsafe handler DestroyButtonView()
	set my native layer to nothing
	put nothing into mButtonView
	put nothing into mButtonProxy
end handler

private unsafe handler UpdateButtonView()
	if mButtonView is nothing then
		return
	end if
	
    /* Set the enabled state of the button to the host property. */
    ObjC_NSButtonSetEnabled(mButtonView, my enabled)

    /* Set the font of the button to the host property. */
    variable tFontToUse as ObjcObject
    MCCanvasFontGetHandle(my font, tFontToUse)
    ObjC_NSButtonSetFont(mButtonView, tFontToUse)

    /* Set the label of the button to mLabel, if not empty; otherwise to the
     * name of the host. */
    variable tLabelToUse as String
    if mLabel is the empty string then
        put my name into tLabelToUse
    else
        put mLabel into tLabelToUse
    end if
	ObjC_NSButtonSetTitle(mButtonView, StringToNSString(tLabelToUse))
end handler

/**/

end widget
