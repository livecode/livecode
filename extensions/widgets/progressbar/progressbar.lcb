/*
Copyright (C) 2015 Runtime Revolution Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/*
Module: com.livecode.extensions.livecode.progressBar

Type: widget

Desciption: This widget is a progress bar. 
This widget has three propeties that can be set: theme; thumbPosition and progress.
*/

-- declaring extension as widget, followed by identifier
widget com.livecode.extensions.livecode.progressBar
--

-- adding metadata to ensure extension displays correctly in livecode
metadata title is "Progress Bar"
metadata author is "Georgia Hutchings"
metadata version is "1.0.5"
--

-- dependency declarations
use com.livecode.canvas
use com.livecode.widget
--

-- property declarations
property theme					get mTheme				set setTheme
property thumbPosition			get mThumbPosition		set setThumbPosition
property progress				get mProgress			set setProgress
--

-- private instance variables
private variable mTheme 			as string
private variable mThumbPosition		as real
private variable mProgress 			as real

private variable mShift 			as real
--

-- constants
constant kBarHeight is 11
constant kProgressHeight is 4

constant kTransparent is [1,1,1,0]
constant kWhite is [1,1,1]
constant kBlack is [0,0,0]
--

----------
-- this handler is called when the widget is saved
public handler OnSave(out rProperties as array)
	put the empty array into rProperties
	
	put mTheme into rProperties["theme"]
	put mThumbPosition into rProperties["thumbPosition"]
	put mProgress into rProperties["progress"]
	
	return rProperties
end handler
----------

----------
-- this handler is called when the widget is loaded
public handler OnLoad(in pProperties as array)
	put pProperties["theme"] into mTheme
	put pProperties["thumbPosition"] into mThumbPosition
	put pProperties["progress"] into mProgress
end handler
----------


----------
-- called when the widget is created
public handler OnCreate()
	-- properties
	put "macos-yosemite" into mTheme
	put 0.0 into mThumbPosition
	put 0.0 into mProgress
	
	-- variables
	put 0.0 into mShift
	
	-- schedule timer
	if mTheme is "macos-10.0" then
		schedule timer in 0.05 seconds
	end if
end handler
----------

----------
-- called whenever LiveCode needs to redraw the widget
public handler OnPaint()
	
	-- getting the thumb position
	updateThumbPosition()
	
	-- draw the rounded rectangle and fill with paint
	variable tBarPath as Path
	put getBarPath() into tBarPath
	set the paint of this canvas to getBarPaint()
	fill tBarPath on this canvas
	--
	
	-- draw the progress bar (background progress bar for yosemite)
	set the paint of this canvas to getProgressPaint()
	fill getProgressPath() on this canvas
	
	-- draw a straight line at the end of the progress bar for macos
	if mTheme is "macos-10.0" or mTheme is "macos-yosemite" and mProgress is not 0.0 then
		set the stroke width of this canvas to getStrokeWidth() 
		set the paint of this canvas to solid paint with getProgressStrokeColor()
		stroke getStrokePath() on this canvas
	end if
	--
	
	-- draw the progress bar for macos-yosemite
	if mTheme is "macos-yosemite" and mProgress is not 0.0 then	
		set the paint of this canvas to getYosemiteProgressPaint()
		fill getYosemiteProgressPath() on this canvas
	end if
	--
	
	-- draw the border by stroking the bar
	set the stroke width of this canvas to 0.9
	set the paint of this canvas to solid paint with getStrokeColor()
	stroke tBarPath on this canvas
	--
	
end handler
----------

----------
-- this handler is called when the timer scheduled with 'schedule timer' fires
public handler OnTimer()
	add 1.0 to mShift
	redraw all
	schedule timer in 0.05 seconds
end handler
----------

----------
-- this handler returns the path for the empty bar
private handler getBarPath()

	if mTheme is "macos-10.0" then
		return rounded rectangle path of rectangle [0, 0, my width, kBarHeight] with radius 2.5
		
	else if mTheme is "macos-yosemite" then
		return rectangle path of rectangle [0, 0, my width, kBarHeight]
		
	else if mTheme is "windows-8" then
		return rectangle path of rectangle [0, kBarHeight/4, my width, 3*kBarHeight/4]
	end if
	
end handler
----------

----------
-- this handler returns the path for the progress bar (background progress path for yosemite)
private handler getProgressPath() as Path

	if mTheme is "macos-10.0" then
		return rounded rectangle path of rectangle [0, 0, mThumbPosition, kBarHeight] with radius 2.5
		
	else if mTheme is "macos-yosemite" then
		return rounded rectangle path of rectangle [2, kBarHeight/2 - kProgressHeight/2, my width - 2, kBarHeight/2 + kProgressHeight/2] with radius 10
	
	else if mTheme is "windows-8" then 
		return rectangle path of rectangle [0, kBarHeight/4, mThumbPosition, 3*kBarHeight/4]
	
	end if
end handler
----------

----------
-- this handler returns the paint for the empty bar
private handler getBarPaint() as Paint

	variable tPaint as Paint
	
	if mTheme is "macos-10.0" then
		variable tStop1
		variable tStop2
		variable tStop3
		
		put gradient stop at 0 with color [160/255, 160/255, 160/255, 0.5] into tStop1
		put gradient stop at (kBarHeight/2)/(my height) with color [160/255, 160/255, 160/255, 0.3] into tStop2
		put gradient stop at kBarHeight/(my height) with color [160/255, 160/255, 160/255, 0.2] into tStop3
		
		put linear gradient with ramp [tStop1, tStop2, tStop3] into tPaint
		scale tPaint by [my height, my width]
		rotate tPaint by 90
		
	else if mTheme is "macos-yosemite" then
		put solid paint with color [224/255, 224/255, 224/255, 0.75] into tPaint
		
	else if mTheme is "windows-8" then
		put solid paint with color [192/255, 192/255, 192/255, 0.75] into tPaint
	end if
	
	return tPaint
end handler
----------

----------
-- this handler returns the paint for the progress
private handler getProgressPaint() as Paint

	variable tPaint as Paint
	
	if mTheme is "macos-10.0" then
		variable tImage as Image
		variable tFile as string
		
		if mThumbPosition is my width then
			
			put "scrollbar_complete.png" into tFile
			put image from resource file tFile into tImage 
			put pattern with tImage into tPaint
			scale tPaint by [my width, 0.9]
			
		else
			put "scrollbar.png" into tFile
			put image from resource file tFile into tImage
			put pattern with tImage into tPaint
			translate tPaint by [-mShift,0]
			
		end if
		
	else if mTheme is "macos-yosemite" then
		put solid paint with color [192/255, 192/255, 192/255, 0.75] into tPaint
		
	else if mTheme is "windows-8" then
		put solid paint with color [62/255, 154/255, 215/255] into tPaint
	end if
	return tPaint
end handler
----------

----------
-- this handler returns the color of the stroke
private handler getStrokeColor() as Color
	if mTheme is "macos-10.0" then
		return color [128/255, 128/255, 128/255]
	else if mTheme is "macos-yosemite" then
		return color kTransparent
	else if mTheme is "windows-8" then
		return color kTransparent
	end if
end handler
----------

----------
-- the following handlers draw straight lines at the end of the mac progress bars
private handler getStrokeWidth() as real
	if mTheme is "macos-10.0" then
		return 0.5
	else if mTheme is "macos-yosemite" then
		return 2
	end if 
end handler

private handler getProgressStrokeColor() as Color
	if mTheme is "macos-10.0" then
		return color [128/255, 128/255, 128/255]
	else if mTheme is "macos-yosemite" then
		return color [51/255, 153/255, 1.0]
	end if 
end handler

private handler getStrokePath() as Path
	if mTheme is "macos-10.0" then
		return line path from point [mThumbPosition, 0] to point [mThumbPosition, kBarHeight]
	else if mTheme is "macos-yosemite" then
		return line path from point [mThumbPosition, kBarHeight/2 - kProgressHeight/2] to point [mThumbPosition, kBarHeight/2 + kProgressHeight/2]
	end if
end handler
----------

----------
-- the following handlers draw the progress bar for macos-yosemite
private handler getYosemiteProgressPath() as Path
	return rounded rectangle path of rectangle [2, kBarHeight/2 - kProgressHeight/2, mThumbPosition, kBarHeight/2 + kProgressHeight/2] with radius 10	
end handler

private handler getYosemiteProgressPaint() as Paint
	return solid paint with color [51/255, 153/255, 1.0]
end handler
----------

----------
-- this handler sets the theme of the widget
/*
Summary: Sets the theme of the progress bar.

Parameters:
pTheme: The name of the theme.

Description:
Sets the theme property of the progress bar to either "macos-10", "macos-yosemite" or "windows-8".
*/
public handler setTheme(in pTheme as string)
	put pTheme into mTheme
	redraw all
end handler
----------

----------
-- this handler sets the thumb position of the progress bar
-- pProgress should be a number between 0 and 100 (a percentage)
/*
Summary: Sets the thumb position of the progress bar.

Parameters:
pProgress: The progress of the bar as a number between 0 and 100.

Description:
Sets the thumbPosition property of the progress bar from the progress as a percentage.
If pProgress is more than 100, then it will be set to 100. If pProgress is less than 0, then it will be set to 0.
*/
public handler setThumbPosition(in pProgress as real) as undefined
	
	variable tProgress as real
	if pProgress > 100 then
		put 100 into tProgress
	else if pProgress < 0 then
		put 0 into tProgress
	else
		put pProgress into tProgress
	end if
	
	if mTheme is "macos-yosemite" then
		put (tProgress/100)*(my width - 4) + 2 into mThumbPosition
	else
		put (tProgress/100)*(my width) into mThumbPosition
	end if
	
	redraw all
end handler
----------

----------
-- this handler sets the progress 
-- pProgress should be a number between 0 and 100 (a percentage)
/*
Summary: Sets the progress of the progress bar.

Parameters:
pProgress: The progress of the bar as a number between 0 and 100.

Description:
Sets the progress property of the progress bar to pProgress.
*/
public handler setProgress(in pProgress as real) as undefined
	put pProgress into mProgress
	redraw all
end handler
----------

----------
-- this handler updates the value of the thumb position
private handler updateThumbPosition() as undefined
	if mTheme is "macos-yosemite" then
		put (mProgress/100)*(my width - 4) + 2 into mThumbPosition
	else
		put (mProgress/100)*(my width) into mThumbPosition
	end if
end handler
----------

end widget