/*
This module specifies the syntax definitions and bindings for
stream input and output operations in modular LiveCode.
*/

module com.livecode.stream

public foreign type Stream binds to "kMCStreamTypeInfo"

--

public foreign handler MCStreamExecGetStandardOutput(out Stdout as Stream) as undefined binds to "<builtin>"

/*
Summary:	Default output stream.
Returns:	The default output stream.

Description:
The default output stream for output from the program.

In command-line programs, this is usually used to output the results
of running the program.  In CGI programs running on servers, this is
usually used to output the data to be sent to the client.

Tags: IO
*/
syntax DefaultOutputStream is expression
	"the" "output" "stream"
begin
	MCStreamExecGetStandardOutput(output)
end syntax

--

public foreign handler MCStreamExecWriteToStream(in Buffer as data, in Destination as Stream) as undefined binds to "<builtin>"
public foreign handler MCStreamExecReadFromStream(in Amount as uint, in Source as Stream) as data binds to "<builtin>"

/*
Summary:	Write data to a stream.

Buffer:	An expression that evaluates to binary data.
Stream:	An expression that evaluates to a stream.

Description:
Write some data to a stream.  If not all of the data can be written,
fails with an error.

>*Warning:* If the stream is able to accept only part of the data,
>some streams will write that part of the data and discard the rest.
>This may cause loss of data.


Tags: IO
*/
syntax WriteToStream is statement
	"write" <Buffer: Expression> "to" "stream" <Destination: Expression>
begin
	MCStreamExecWriteToStream(Buffer, Destination)
end syntax

/*
Summary:	Read data from a stream.

Amount:	An expression that evaluates to a number.
Stream:	An expression that evaluates to a stream.

Description:
Read a specific amount of data from a stream.  If there is not enough
data available in the stream, fails with an error.

>*Warning:* Some streams will discard successfully-read data if there
>wasn't enough data to fully satisfy the request.  This may cause loss
>of data.

*/
syntax ReadFromStream is statement
	"read" <Amount: Expression> "bytes" "from" "stream" <Source: Expression>
begin
    MCStreamExecReadFromStream(Amount, Source)
end syntax

--

end module