/*
Copyright (C) 2015 LiveCode Ltd.

This file is part of LiveCode.

LiveCode is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License v3 as published by the Free
Software Foundation.

LiveCode is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
for more details.

You should have received a copy of the GNU General Public License
along with LiveCode.  If not see <http://www.gnu.org/licenses/>.  */

/**
This library provides low-level system functionality for modular
LiveCode programs.
*/

module com.livecode.system

use com.livecode.foreign

----------------------------------------------------------------
-- Platform identification
----------------------------------------------------------------

public foreign handler MCSystemExecGetOperatingSystem(out Platform as String) returns nothing binds to "<builtin>"

/**
Summary:	The operating system

Example:
	if the operating system is "linux" then
		- Platform specific-code
	end if

Description:
Returns a string describing the operating system that LiveCode is
running on.  The possible values are:

* "windows" - 32-bit and 64-bit Windows
* "mac" - Desktop OS X
* "ios" - iOS (iPhone and iPad)
* "android" - Android Linux devices
* "linux" - All other Linux platforms

Tags: System
*/
syntax OperatingSystem is expression
	"the" "operating" "system"
begin
	MCSystemExecGetOperatingSystem(output)
end syntax

----------------------------------------------------------------
-- Exit / Quit
----------------------------------------------------------------

foreign handler __Exit(in ExitStatus as CInt) returns nothing binds to "exit"

public handler MCSystemExecQuit() returns nothing
	unsafe
		__Exit(0)
	end unsafe
end handler

public handler MCSystemExecQuitWithStatus(in Status as Number)
	unsafe
		__Exit(Status)
	end unsafe
end handler

/**
Summary:	Quit the application

Example:
	-- Quit, indicating that the application ran successfully (code 0)
	quit

	-- Quit, with a specific error code
	quit with status 42

Description:
Exit the program immediately and unconditionally, returning a status
number to the operating system.  If no status is provided, the default
value of 0 is used.

Tags: System
*/
syntax QuitWithStatus is statement
	"quit" [ "with" "status" <Status: Expression> ]
begin
	MCSystemExecQuit()
	MCSystemExecQuitWithStatus(Status)
end syntax

----------------------------------------------------------------
-- Command-line information
----------------------------------------------------------------

public foreign handler MCSystemExecGetCommandName (out CommandName as String) returns nothing binds to "<builtin>"
public foreign handler MCSystemExecGetCommandArguments (out CommandArguments as List) returns nothing binds to "<builtin>"

/**
Summary:	The command name

Example:
	-- Program that only succeeds if it's run as the "true"
	-- command.
	variable tCommand as String
	put the command name into tCommand
	if tCommand ends with "true" then
		quit with status 0
	else
		quit with status 1
	end if

Description:
Evaluates to the name that was used to execute the program, possibly
including path information.
*/
syntax CommandName is expression
	"the" "command" "name"
begin
	MCSystemExecGetCommandName(output)
end syntax

/**
Summary:	The command arguments

Example:

	-- Program that only succeeds if the first argument is "--success"
	if the command arguments is not empty then
		if the first element of the command arguments is "--success" then
			quit with status 0
		end if
	end if
	quit with status 1

Description:
Evaluates to a list of command-line arguments passed to the program.
Some arguments may not be passed in if they are "used up" by the
LiveCode run-time environment (for example, the LiveCode IDE will
detect and "use" the `-mmap` argument).

**Note:** No filename conversion is performed on command-line
arguments.  Command-line arguments that should be interpreted as
filenames should <CommandArguments | specially processed>.

Tags: System
*/
syntax CommandArguments is expression
	"the" "command" "arguments"
begin
	MCSystemExecGetCommandArguments(output)
end syntax

--

end module
