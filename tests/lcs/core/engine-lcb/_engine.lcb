library com.livecode.lcs_tests.core.extensions.engine

use com.livecode.engine

public handler TestExtensionDelimiterAccess()
	return [the item delimiter, the line delimiter, the column delimiter, the row delimiter]
end handler

public handler TestExtensionExecuteScript_Do(in pScript as String, in pTarget as String, in pParams as List)
	if pParams is the empty list and pTarget is the empty string then
		execute script pScript
	else if pParams is the empty list and pTarget is not the empty string then
		resolve script object pTarget
		execute script pScript in the result
	else if pParams is not the empty list and pTarget is the empty string then
		execute script pScript with pParams
	else
		resolve script object pTarget
		execute script pScript in the result with pParams
	end if
	return the result
end handler

public handler TestExtensionLog_String()
	log "log"
end handler

public handler TestExtensionLog_Integer()
	log 1
end handler

public handler TestExtensionLog_List()
	log [1,2,3]
end handler

public handler TestExtensionLog_Array()
	log {"a": 1}
end handler

public handler TestExtensionLog_ScriptObject()
	resolve script object "button 1"
	log the result
end handler

public handler TestExtensionLog_DeletedScriptObject()
	variable tScriptObject as ScriptObject
	resolve script object "button 1" into tScriptObject
	execute script "delete button 1"
	log tScriptObject
end handler

public handler TestEngine_Caller(in pCallback as String)
	send pCallback to the caller
end handler

public handler TestEngine_ResolveFile(in pPath as String, in pRelativeObject as String) returns String
	if pRelativeObject is not empty then
		variable tObject as ScriptObject
		resolve script object pRelativeObject
		put the result into tObject
		if tObject exists then
			resolve file pPath relative to tObject
			return the result
		else
			throw "object not found:" && pRelativeObject
		end if
	else
		resolve file pPath
		return the result
	end if
end handler

end library
